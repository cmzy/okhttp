apply plugin: 'me.champeau.gradle.japicmp'

jar {
  manifest {
    attributes('Automatic-Module-Name': 'mOkhttp3')
  }
}

sourceSets {
  main.java.srcDirs += "$buildDir/generated/sources/java-templates/java/main"
}

compileKotlin {
  dependsOn 'copyJavaTemplates'
}

task copyJavaTemplates(type: Copy) {
  from 'src/main/java-templates'
  into "$buildDir/generated/sources/java-templates/java/main"
  expand('projectVersion': "${project.version}")
  filteringCharset = 'UTF-8'
}

dependencies {
  api deps.okio
  api deps.kotlinStdlib
  compileOnly deps.android
  compileOnly deps.bouncycastle
  compileOnly deps.bouncycastletls
  compileOnly deps.conscrypt
  compileOnly deps.openjsse
  compileOnly deps.jsr305
  compileOnly deps.animalSniffer

  testImplementation project(':okhttp-testing-support')
  testImplementation project(':okhttp-tls')
  testImplementation project(':okhttp-urlconnection')
  testImplementation project(':mockwebserver')
  testImplementation project(':okhttp-logging-interceptor')
  testImplementation deps.conscrypt
  testImplementation deps.junit
  testImplementation deps.assertj
  testImplementation deps.openjsse
  testCompileOnly deps.jsr305
}

afterEvaluate { project ->
  project.tasks.dokka {
    outputDirectory = "$rootDir/docs/4.x"
    outputFormat = 'gfm'
  }
}

task japicmp(type: me.champeau.gradle.japicmp.JapicmpTask, dependsOn: 'jar') {
  oldClasspath = files(baselineJar(project, baselineVersion))
  newClasspath = files(jar.archivePath)
  onlyBinaryIncompatibleModified = true
  failOnModification = false
  txtOutputFile = file("$buildDir/reports/japi.txt")
  ignoreMissingClasses = true
  includeSynthetic = true
  packageExcludes = [
      'mOkhttp3.internal',
      'mOkhttp3.internal.annotations',
      'mOkhttp3.internal.cache',
      'mOkhttp3.internal.cache2',
      'mOkhttp3.internal.connection',
      'mOkhttp3.internal.http',
      'mOkhttp3.internal.http1',
      'mOkhttp3.internal.http2',
      'mOkhttp3.internal.io',
      'mOkhttp3.internal.platform',
      'mOkhttp3.internal.proxy',
      'mOkhttp3.internal.publicsuffix',
      'mOkhttp3.internal.tls',
      'mOkhttp3.internal.ws',
  ]
  classExcludes = [
      // Package-private in 3.x, internal in 4.0.0:
      'mOkhttp3.Cache$CacheResponseBody$1',
      'mOkhttp3.RealCall$AsyncCall',
  ]
  methodExcludes = [
      // Became 'final' despite a non-final enclosing class in 4.0.0:
      'mOkhttp3.OkHttpClient#authenticator()',
      'mOkhttp3.OkHttpClient#cache()',
      'mOkhttp3.OkHttpClient#callTimeoutMillis()',
      'mOkhttp3.OkHttpClient#certificatePinner()',
      'mOkhttp3.OkHttpClient#connectionPool()',
      'mOkhttp3.OkHttpClient#connectionSpecs()',
      'mOkhttp3.OkHttpClient#connectTimeoutMillis()',
      'mOkhttp3.OkHttpClient#cookieJar()',
      'mOkhttp3.OkHttpClient#dispatcher()',
      'mOkhttp3.OkHttpClient#dns()',
      'mOkhttp3.OkHttpClient#eventListenerFactory()',
      'mOkhttp3.OkHttpClient#followRedirects()',
      'mOkhttp3.OkHttpClient#followSslRedirects()',
      'mOkhttp3.OkHttpClient#hostnameVerifier()',
      'mOkhttp3.OkHttpClient#interceptors()',
      'mOkhttp3.OkHttpClient#networkInterceptors()',
      'mOkhttp3.OkHttpClient#pingIntervalMillis()',
      'mOkhttp3.OkHttpClient#protocols()',
      'mOkhttp3.OkHttpClient#proxy()',
      'mOkhttp3.OkHttpClient#proxyAuthenticator()',
      'mOkhttp3.OkHttpClient#proxySelector()',
      'mOkhttp3.OkHttpClient#readTimeoutMillis()',
      'mOkhttp3.OkHttpClient#retryOnConnectionFailure()',
      'mOkhttp3.OkHttpClient#socketFactory()',
      'mOkhttp3.OkHttpClient#sslSocketFactory()',
      'mOkhttp3.OkHttpClient#writeTimeoutMillis()',
      'mOkhttp3.OkHttpClient#writeTimeoutMillis()',
      'mOkhttp3.Request$Builder#delete()',
  ]
}
check.dependsOn(japicmp)
